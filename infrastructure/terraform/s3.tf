resource "aws_kms_key" "artifacts" {
  description             = "KMS key for risk lab artifact bucket"
  deletion_window_in_days = 30
  enable_key_rotation     = true

  tags = {
    Name    = "deployguard-risk-lab-artifacts"
    Service = "artifact-storage"
  }
}

resource "aws_s3_bucket" "artifacts" {
  bucket = "${var.bucket_prefix}-${var.environment}-artifacts"

  tags = {
    Environment = var.environment
    Name        = "deployguard-risk-lab-artifacts"
    Service     = "artifact-storage"
  }
}

resource "aws_s3_bucket_public_access_block" "artifacts" {
  bucket = aws_s3_bucket.artifacts.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

resource "aws_s3_bucket_server_side_encryption_configuration" "artifacts" {
  bucket = aws_s3_bucket.artifacts.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm     = "aws:kms"
      kms_master_key_id = aws_kms_key.artifacts.arn
    }
  }
}

resource "aws_s3_bucket_versioning" "artifacts" {
  bucket = aws_s3_bucket.artifacts.id

  versioning_configuration {
    status = "Suspended"
  }
}
